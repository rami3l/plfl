-- https://plfa.github.io/Lambda/

import Mathlib.Tactic

set_option tactic.simp.trace true

open String

def Sym : Type := String deriving BEq, DecidableEq, Repr

-- https://plfa.github.io/Lambda/#syntax-of-terms
inductive Term where
| var : Sym â†’ Term
| lam : Sym â†’ Term â†’ Term
| ap : Term â†’ Term â†’ Term
| zero : Term
| succ : Term â†’ Term
| case : Term â†’ Term â†’ Sym â†’ Term â†’ Term
| mu : Sym â†’ Term â†’ Term
deriving BEq, DecidableEq, Repr

namespace Term
  notation:50 " Æ› " v " : " d => lam v d
  notation:50 " Î¼ " v " : " d => mu v d
  notation:max " ğŸ˜? " e " [zero: " o " |succ " n " : " i " ] " => case e o n i
  infixr:min " $ " => ap
  infixl:70 " â–¡ " => ap
  prefix:80 " Î¹ " => succ
  prefix:90 " ` " => var
  notation " ğŸ˜ " => zero

  example : Term := `"foo"
  example : Term := ğŸ˜? `"bar" [zero: ğŸ˜ |succ "n" : Î¹ ğŸ˜]

  @[simp]
  def ofNat | 0 => zero | n + 1 => succ <| ofNat n
  instance : Coe â„• Term where coe := ofNat
  instance : OfNat Term n where ofNat := ofNat n

  example : Term := 1
  example : Term := 42

  abbrev add : Term := Î¼ "+" : Æ› "m" : Æ› "n" : ğŸ˜? `"m" [zero: `"n" |succ "m": Î¹ (`"+" â–¡ `"m" â–¡ `"n")]
  -- https://plfa.github.io/Lambda/#exercise-mul-recommended
  abbrev mul : Term := Î¼ "*" : Æ› "m" : Æ› "n" : ğŸ˜? `"m" [zero: ğŸ˜ |succ "m": add â–¡ `"n" $ `"*" â–¡ `"m" â–¡ `"n"]

  -- Church encoding...
  abbrev succ_c : Term := Æ› "n" : Î¹ `"n"
  abbrev one_c : Term := Æ› "s" : Æ› "z" : `"s" $ `"z"
  abbrev two_c : Term := Æ› "s" : Æ› "z" : `"s" $ `"s" $ `"z"
  abbrev add_c : Term := Æ› "m" : Æ› "n" : Æ› "s" : Æ› "z" : `"m" â–¡ `"s" $ `"n" â–¡ `"s" â–¡ `"z"
  -- https://plfa.github.io/Lambda/#exercise-mul%E1%B6%9C-practice
  abbrev mul_c : Term := Æ› "m" : Æ› "n" : Æ› "s" : Æ› "z" : `"m" â–¡ (`"n" â–¡ `"s") â–¡ `"z"
end Term

-- https://plfa.github.io/Lambda/#values
inductive Value : Term â†’ Type where
| lam : Value (Æ› v : d)
| zero: Value ğŸ˜
| succ: Value n â†’ Value (Î¹ n)
deriving BEq, DecidableEq, Repr

namespace Value
  notation " VğŸ˜ " => zero

  @[simp]
  def ofNat : (n : â„•) â†’ Value (Term.ofNat n)
  | 0 => VğŸ˜
  | n + 1 => succ <| ofNat n

  -- instance : CoeDep â„• n (Value â†‘n) where coe := ofNat n
  -- instance : OfNat (Value (Term.ofNat n)) n where ofNat := ofNat n
end Value

-- https://plfa.github.io/Lambda/#substitution
namespace Term
  /--
  `x.subst y v` substitutes term `v` for all free occurrences of variable `y` in term `x`.
  -/
  @[simp]
  def subst : Term â†’ Sym â†’ Term â†’ Term
  | ` x, y, v => if x = y then v else ` x
  | Æ› x : n, y, v => if x = y then Æ› x : n else Æ› x : n.subst y v
  | ap l m, y, v => l.subst y v $ m.subst y v
  | ğŸ˜, _, _ => ğŸ˜
  | Î¹ n, y, v => Î¹ (n.subst y v)
  | ğŸ˜? l [zero: m |succ x: n], y, v => if x = y
      then ğŸ˜? l.subst y v [zero: m.subst y v |succ x: n]
      else ğŸ˜? l.subst y v [zero: m.subst y v |succ x: n.subst y v]
  | Î¼ x : n, y, v => if x = y then Î¼ x : n else Î¼ x : n.subst y v

  notation:90 x " [ " y " := " v " ] " => subst x y v

  -- https://plfa.github.io/Lambda/#examples
  example
  : (Æ› "z" : `"s" â–¡ `"s" â–¡ `"z")["s" := succ_c]
  = (Æ› "z" : succ_c â–¡ succ_c â–¡ `"z") := rfl

  example : (succ_c â–¡ succ_c â–¡ `"z")["z" := ğŸ˜] = succ_c â–¡ succ_c â–¡ ğŸ˜ := rfl
  example : (Æ› "x" : `"y")["y" := ğŸ˜] = (Æ› "x" : ğŸ˜) := rfl
  example : (Æ› "x" : `"x")["x" := ğŸ˜] = (Æ› "x" : `"x") := rfl
  example : (Æ› "y" : `"y")["x" := ğŸ˜] = (Æ› "y" : `"y") := rfl

  -- https://plfa.github.io/Lambda/#quiz
  example
  : (Æ› "y" : `"x" $ Æ› "x" : `"x")["x" := ğŸ˜]
  = (Æ› "y" : ğŸ˜ $ Æ› "x" : `"x")
  := rfl

  -- https://plfa.github.io/Lambda/#reduction
  /--
  `Reduce t t'` says that `t` reduces to `t'`.
  -/
  inductive Reduce : Term â†’ Term â†’ Type where
  | lam_Î² : Value v â†’ Reduce ((Æ› x : n) â–¡ v) (n[x := v])
  | ap_Î¾â‚ : Reduce l l' â†’ Reduce (l â–¡ m) (l' â–¡ m)
  | ap_Î¾â‚‚ : Value v â†’ Reduce m m' â†’ Reduce (v â–¡ m) (v â–¡ m')
  | zero_Î² : Reduce (ğŸ˜? ğŸ˜ [zero: m |succ x : n]) m
  | succ_Î² : Value v â†’ Reduce (ğŸ˜? Î¹ v [zero: m |succ x : n]) (n[x := v])
  | succ_Î¾ : Reduce m m' â†’ Reduce (Î¹ m) (Î¹ m')
  | case_Î¾ : Reduce l l' â†’ Reduce (ğŸ˜? l [zero: m |succ x : n]) (ğŸ˜? l' [zero: m |succ x : n])
  | mu_Î² : Reduce (Î¼ x : m) (m[x := Î¼ x : m])
  deriving Repr

  infix:40 " â€”â†’ " => Reduce
end Term

namespace Term.Reduce
  -- https://plfa.github.io/Lambda/#quiz-1
  example : (Æ› "x" : `"x") â–¡ (Æ› "x" : `"x") â€”â†’ (Æ› "x" : `"x") := by
    apply lam_Î²; exact Value.lam

  example : (Æ› "x" : `"x") â–¡ (Æ› "x" : `"x") â–¡ (Æ› "x" : `"x") â€”â†’ (Æ› "x" : `"x") â–¡ (Æ› "x" : `"x") := by
    apply ap_Î¾â‚; apply lam_Î²; exact Value.lam

  example : two_c â–¡ succ_c â–¡ ğŸ˜ â€”â†’ (Æ› "z" : succ_c $ succ_c $ `"z") â–¡ ğŸ˜ := by
    unfold two_c; apply ap_Î¾â‚; apply lam_Î²; exact Value.lam

  -- https://plfa.github.io/Lambda/#reflexive-and-transitive-closure
  /--
  A reflexive and transitive closure,
  defined as a sequence of zero or more steps of the underlying relation `â€”â†’`.
  -/
  inductive Clos : Term â†’ Term â†’ Type where
  | nil : Clos m m
  | cons : (l â€”â†’ m) â†’ Clos m n â†’ Clos l n
  deriving Repr

  infix:20 " â€”â†  " => Clos

  namespace Clos
    @[simp]
    def length : (m â€”â†  n) â†’ Nat
    | nil => 0
    | cons _ cdr => 1 + cdr.length

    abbrev one (car : m â€”â†’ n) : (m â€”â†  n) := cons car nil
    instance : Coe (m â€”â†’ n) (m â€”â†  n) where coe := one

    @[simp]
    def trans : (l â€”â†  m) â†’ (m â€”â†  n) â†’ (l â€”â†  n)
    | nil, c => c
    | cons h c, c' => cons h <| c.trans c'

    instance isTrans : Trans Clos Clos Clos where
      trans := trans
  end Clos

  inductive Clos' : Term â†’ Term â†’ Type where
  | refl : Clos' m m
  | step : (m â€”â†’ n) â†’ Clos' m n
  | trans : Clos' l m â†’ Clos' m n â†’ Clos' l n

  infix:20 " â€”â† ' " => Clos'

  @[simp]
  def Clos.to_clos' : (m â€”â†  n) â†’ (m â€”â† ' n) := by
    intro
    | nil => exact Clos'.refl
    | cons h h' => exact Clos'.trans (Clos'.step h) h'.to_clos'

  @[simp]
  def Clos'.to_clos : (m â€”â† ' n) â†’ (m â€”â†  n) := by
    intro
    | refl => exact Clos.nil
    | step h => exact â†‘h
    | trans h h' => apply Clos.trans <;> (apply to_clos; assumption)

  -- https://plfa.github.io/Lambda/#exercise-practice
  lemma Clos.to_clos'_left_inv : âˆ€ {x : m â€”â†  n}, x.to_clos'.to_clos = x := by
    intro
    | nil => rfl
    | cons car cdr => simp_all; exact to_clos'_left_inv (x := cdr)

  lemma Clos.to_clos'_inj
  : @Function.Injective (m â€”â†  n) (m â€”â† ' n) Clos.to_clos'
  := by
    unfold Function.Injective
    intro a b h
    apply_fun Clos'.to_clos at h
    rwa [â†to_clos'_left_inv (x := a), â†to_clos'_left_inv (x := b)]

  instance Clos.embeds_in_clos' : (m â€”â†  n) â†ª (m â€”â† ' n) where
    toFun := to_clos'
    inj' := to_clos'_inj
end Term.Reduce

-- https://plfa.github.io/Lambda/#confluence
section confluence
  open Term.Reduce Term.Reduce.Clos

  -- `Î£` is used instead of `âˆƒ` because it's a `Type` that exists, not a `Prop`.
  def Diamond : Type := âˆ€ â¦ƒl m nâ¦„, (l â€”â†’ m) â†’ (l â€”â†’ n) â†’ (Î£ p, (m â€”â†  p) Ã— (n â€”â†  p))
  def Confluence : Type := âˆ€ â¦ƒl m nâ¦„, (l â€”â†  m) â†’ (l â€”â†  n) â†’ (Î£ p, (m â€”â†  p) Ã— (n â€”â†  p))
  def Deterministic : Prop := âˆ€ â¦ƒl m nâ¦„, (l â€”â†’ m) â†’ (l â€”â†’ n) â†’ (m = n)

  theorem Deterministic.to_diamond : Deterministic â†’ Diamond := by
    unfold Deterministic Diamond; intro h l m n lm ln
    have heq := h lm ln; simp_all
    exists n; exact âŸ¨nil, nilâŸ©

  theorem Deterministic.to_confluence : Deterministic â†’ Confluence
  | h, l, m, n, lm, ln => by match lm, ln with
    | nil, nil => exists n; exact âŸ¨ln, lnâŸ©
    | nil, c@(cons _ _) => exists n; exact âŸ¨c, nilâŸ©
    | c@(cons _ _), nil => exists m; exact âŸ¨nil, câŸ©
    | cons car cdr, cons car' cdr' =>
      have := h car car'; subst this
      exact to_confluence h cdr cdr'
end confluence

-- https://plfa.github.io/Lambda/#examples-1
section examples
  open Term Term.Reduce Term.Reduce.Clos

  example : two_c â–¡ succ_c â–¡ ğŸ˜ â€”â†  2 := calc
    two_c â–¡ succ_c â–¡ ğŸ˜
    -- `Clos.one` means that we are reducing just by a single step.
    _ â€”â†  (Æ› "z" : succ_c $ succ_c $ `"z") â–¡ ğŸ˜ := Clos.one <| by apply ap_Î¾â‚; apply lam_Î²; exact Value.lam
    _ â€”â†  (succ_c $ succ_c $ ğŸ˜) := Clos.one <| by apply lam_Î²; exact Value.zero
    _ â€”â†  succ_c â–¡ 1 := Clos.one <| by apply ap_Î¾â‚‚; apply Value.lam; apply lam_Î²; exact Value.zero
    _ â€”â†  2 := Clos.one <| by apply lam_Î²; exact Value.ofNat 1

  -- https://plfa.github.io/Lambda/#exercise-plus-example-practice
  example : add â–¡ 1 â–¡ 1 â€”â†  2 := calc
    add â–¡ 1 â–¡ 1
    _ â€”â†  (Æ› "m" : Æ› "n" : ğŸ˜? `"m" [zero: `"n" |succ "m": Î¹ (add â–¡ `"m" â–¡ `"n")]) â–¡ 1 â–¡ 1
      := Clos.one <| by apply ap_Î¾â‚; apply ap_Î¾â‚; apply mu_Î²
    _ â€”â†  (Æ› "n" : ğŸ˜? 1 [zero: `"n" |succ "m": Î¹ (add â–¡ `"m" â–¡ `"n")]) â–¡ 1
      := Clos.one <| by apply ap_Î¾â‚; apply lam_Î²; exact Value.ofNat 1
    _ â€”â†  ğŸ˜? 1 [zero: 1 |succ "m": Î¹ (add â–¡ `"m" â–¡ 1)]
      := Clos.one <| lam_Î² <| Value.ofNat 1
    _ â€”â†  Î¹ (add â–¡ ğŸ˜ â–¡ 1)
      := Clos.one <| succ_Î² Value.zero
    _ â€”â†  Î¹ ((Æ› "m" : Æ› "n" : ğŸ˜? `"m" [zero: `"n" |succ "m": Î¹ (add â–¡ `"m" â–¡ `"n")]) â–¡ ğŸ˜ â–¡ 1)
      := Clos.one <| by apply succ_Î¾; apply ap_Î¾â‚; apply ap_Î¾â‚; apply mu_Î²
    _ â€”â†  Î¹ ((Æ› "n" : ğŸ˜? ğŸ˜ [zero: `"n" |succ "m": Î¹ (add â–¡ `"m" â–¡ `"n")]) â–¡ 1)
      := Clos.one <| by apply succ_Î¾; apply ap_Î¾â‚; apply lam_Î²; exact VğŸ˜
    _ â€”â†  Î¹ (ğŸ˜? ğŸ˜ [zero: 1 |succ "m": Î¹ (add â–¡ `"m" â–¡ 1)])
      := Clos.one <| by apply succ_Î¾; apply lam_Î²; exact Value.ofNat 1
    _ â€”â†  2 := Clos.one <| succ_Î¾ zero_Î²
end examples

-- https://plfa.github.io/Lambda/#syntax-of-types
inductive Ty where
| nat
| fn : Ty â†’ Ty â†’ Ty
deriving BEq, DecidableEq, Repr

namespace Ty
  notation "â„•t" => nat
  infixr:70 " =â‡’ " => fn

  example : Ty := (â„•t =â‡’ â„•t) =â‡’ â„•t

  @[simp]
  theorem t_to_t'_ne_t (t t' : Ty) : (t =â‡’ t') â‰  t := by
    by_contra h; match t with
    | nat => trivial
    | fn ta tb => injection h; have := t_to_t'_ne_t ta tb; contradiction
end Ty

-- https://plfa.github.io/Lambda/#contexts
def Context : Type := List (Sym Ã— Ty)

namespace Context
  open Term

  def nil : Context := []
  def extend : Context â†’ Sym â†’ Ty â†’ Context | c, s, ts => âŸ¨s, tsâŸ© :: c

  notation " âˆ… " => nil

  -- The goal is to make `_:<_â¦‚_` work like an `infixl`.
  -- https://matklad.github.io/2020/04/13/simple-but-powerful-pratt-parsing.html#From-Precedence-to-Binding-Power
  notation:50 c " :< " s:51 " â¦‚ " t:51 => extend c s t

  example {Î“ : Context} {s : Sym} {ts : Ty} : Context := Î“ :< s â¦‚ ts

  -- https://plfa.github.io/Lambda/#lookup-judgment
  /--
  A lookup judgement.
  `Lookup c s ts` means that `s` is of type `ts` by _looking up_ the context `c`.
  -/
  @[aesop safe [constructors, cases]]
  inductive Lookup : Context â†’ Sym â†’ Ty â†’ Type where
  | z : Lookup (Î“ :< x â¦‚ tx) x tx
  | s : x â‰  y â†’ Lookup Î“ x tx â†’ Lookup (Î“ :< y â¦‚ ty) x tx
  deriving DecidableEq

  notation:40 c " âˆ‹ " s " â¦‚ " t:51 => Lookup c s t

  example
  : âˆ… :< "x" â¦‚ â„•t =â‡’ â„•t :< "y" â¦‚ â„•t :< "z" â¦‚ â„•t
  âˆ‹ "x" â¦‚ â„•t =â‡’ â„•t
  := open Lookup in by
    apply s _; apply s _; apply z; repeat trivial

  -- https://plfa.github.io/Lambda/#lookup-is-functional
  @[simp]
  theorem Lookup.functional : Î“ âˆ‹ x â¦‚ tx â†’ Î“ âˆ‹ x â¦‚ tx' â†’ tx = tx' := by
    intro
    | z, z => rfl
    | z, s _ e => trivial
    | s _ e, z => trivial
    | s _ e, s _ e' => exact functional e e'

  -- https://plfa.github.io/Lambda/#typing-judgment
  /--
  A general typing judgement.
  `IsTy c t tt` means that `t` can be inferred to be of type `tt` in the context `c`.
  -/
  inductive IsTy : Context â†’ Term â†’ Ty â†’ Type where
  | ty_var : Î“ âˆ‹ x â¦‚ tx â†’ IsTy Î“ (` x) tx
  | ty_lam : IsTy (Î“ :< x â¦‚ tx) n tn â†’ IsTy Î“ (Æ› x : n) (tx =â‡’ tn)
  | ty_ap : IsTy Î“ l (tx =â‡’ tn) â†’ IsTy Î“ x tx â†’ IsTy Î“ (l â–¡ x) tn
  | ty_zero : IsTy Î“ ğŸ˜ â„•t
  | ty_succ : IsTy Î“ n â„•t â†’ IsTy Î“ (Î¹ n) â„•t
  | ty_case : IsTy Î“ l â„•t â†’ IsTy Î“ m t â†’ IsTy (Î“ :< x â¦‚ â„•t) n t â†’ IsTy Î“ (ğŸ˜? l [zero: m |succ x: n]) t
  | ty_mu : IsTy (Î“ :< x â¦‚ t) m t â†’ IsTy Î“ (Î¼ x : m) t
  deriving DecidableEq

  notation:40 c " âŠ¢ " t " â¦‚ " tt:51 => IsTy c t tt

  /--
  `NoTy c t` means that `t` cannot be inferred to be any type in the context `c`.
  -/
  abbrev NoTy (c : Context) (t : Term) : Prop := âˆ€ {tt}, IsEmpty (c âŠ¢ t â¦‚ tt)

  infix:40 " âŠ¬ " => NoTy

  -- https://github.com/arthurpaulino/lean4-metaprogramming-book/blob/d6a227a63c55bf13d49d443f47c54c7a500ea27b/md/main/tactics.md#tactics-by-macro-expansion
  /--
  `lookup_var` validates the type of a variable by looking it up in the current context.
  This tactic fails when the lookup fails.
  -/
  syntax "lookup_var" : tactic
  macro_rules
  | `(tactic| lookup_var) =>
    `(tactic| apply IsTy.ty_var; repeat (first | apply Lookup.s (by trivial) | exact Lookup.z))

  -- Inform `trivial` of our new tactic.
  macro_rules | `(tactic| trivial) => `(tactic| lookup_var)

  open IsTy

  -- https://plfa.github.io/Lambda/#quiz-2
  lemma twice_ty : Î“ âŠ¢ (Æ› "s" : `"s" $ `"s" $ ğŸ˜) â¦‚ ((â„•t =â‡’ â„•t) =â‡’ â„•t) := by
    apply ty_lam; apply ty_ap
    Â· trivial
    Â· apply ty_ap
      Â· trivial
      Â· exact ty_zero

  theorem two_ty : Î“ âŠ¢ (Æ› "s" : `"s" $ `"s" $ ğŸ˜) â–¡ succ_c â¦‚ â„•t := by
    apply ty_ap twice_ty
    Â· apply ty_lam; apply ty_succ; trivial

  -- https://plfa.github.io/Lambda/#derivation
  abbrev NatC (t : Ty) : Ty := (t =â‡’ t) =â‡’ t =â‡’ t

  theorem two_c_ty : Î“ âŠ¢ two_c â¦‚ NatC t := by
    apply ty_lam; apply ty_lam; apply ty_ap
    Â· trivial
    Â· apply ty_ap <;> trivial

  def add_ty : Î“ âŠ¢ add â¦‚ â„•t =â‡’ â„•t =â‡’ â„•t := by
    apply ty_mu; apply ty_lam; apply ty_lam; apply ty_case <;> try trivial
    Â· apply ty_succ; apply ty_ap <;> try trivial
      Â· apply ty_ap <;> trivial

  theorem add_c_ty : Î“ âŠ¢ add_c â¦‚ NatC t =â‡’ NatC t =â‡’ NatC t := by
    repeat apply ty_lam <;> try trivial
    Â· repeat apply ty_ap <;> try trivial

  -- https://plfa.github.io/Lambda/#exercise-mul-recommended-1
  def mul_ty : Î“ âŠ¢ mul â¦‚ â„•t =â‡’ â„•t =â‡’ â„•t := by
    -- TODO: Can we simplify thisğŸ˜?
    apply ty_mu; apply ty_lam; apply ty_lam; apply ty_case
    Â· trivial
    Â· exact ty_zero
    Â· apply ty_ap
      Â· apply ty_ap <;> try trivial
        Â· apply ty_mu; apply ty_lam; apply ty_lam; apply ty_case <;> try trivial
          Â· apply ty_succ; apply ty_ap <;> try trivial
            Â· apply ty_ap <;> trivial
      Â· repeat apply ty_ap; repeat trivial

  -- https://plfa.github.io/Lambda/#exercise-mul%E1%B6%9C-practice-1
  theorem mul_c_ty : Î“ âŠ¢ mul_c â¦‚ NatC t =â‡’ NatC t =â‡’ NatC t := by
    repeat apply ty_lam <;> try trivial
    Â· repeat apply ty_ap <;> try trivial
end Context

section examples
  open Term Context Lookup IsTy

  -- https://plfa.github.io/Lambda/#non-examples
  example : âˆ… âŠ¬ ğŸ˜ â–¡ 1 := by
    by_contra h; simp_all; cases h.some; contradiction

  abbrev ill_lam := Æ› "x" : `"x" â–¡ `"x"

  lemma nty_ill_lam : âˆ… âŠ¬ ill_lam := by
    by_contra h; simp_all
    let ty_lam (ty_ap (ty_var hx) (ty_var hx')) := h.some
    have := Lookup.functional hx hx'; simp_all

  -- https://plfa.github.io/Lambda/#quiz-3
  example : âˆ… :< "y" â¦‚ â„•t =â‡’ â„•t :< "x" â¦‚ â„•t âŠ¢ `"y" â–¡ `"x" â¦‚ â„•t := by
    apply ty_ap <;> trivial

  example : âˆ… :< "y" â¦‚ â„•t =â‡’ â„•t :< "x" â¦‚ â„•t âŠ¬ `"x" â–¡ `"y" := by
    by_contra h; simp_all
    let âŸ¨htâŸ© := h
    cases ht; rename_i hy hx
    Â· cases hx; rename_i ty hx
      Â· cases hx; contradiction

  example : âˆ… :< "y" â¦‚ â„•t =â‡’ â„•t âŠ¢ Æ› "x" : `"y" â–¡ `"x" â¦‚ â„•t =â‡’ â„•t := by
    apply ty_lam; apply ty_ap <;> trivial

  example : âˆ… :< "x" â¦‚ tx âŠ¬ `"x" â–¡ `"x" := by
    by_contra h; simp_all
    let âŸ¨htâŸ© := h
    cases ht; rename_i hx
    Â· cases hx; rename_i hx
      Â· cases hx <;> contradiction

  example
  : âˆ… :< "x" â¦‚ â„•t =â‡’ â„•t :< "y" â¦‚ â„•t =â‡’ â„•t
  âŠ¢ Æ› "z" : (`"x" $ `"y" $ `"z") â¦‚ â„•t =â‡’ â„•t
  := by
    apply ty_lam; apply ty_ap <;> try trivial
    Â· apply ty_ap <;> trivial
end examples
