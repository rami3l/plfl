-- https://plfa.github.io/Untyped/

import Plfl.Init

import Mathlib.Tactic

namespace Untyped

-- https://plfa.github.io/Untyped/#types
inductive Ty where
| star: Ty
deriving BEq, DecidableEq, Repr

namespace Notation
  scoped notation "âœ¶" => Ty.star
end Notation

open Notation

-- https://plfa.github.io/Untyped/#exercise-type-practice
instance : Ty â‰ƒ Unit where
  toFun _ := ()
  invFun _ := âœ¶
  left_inv _ := by simp only
  right_inv _ := by simp only

instance : Unique Ty where
  default := âœ¶
  uniq := by simp only [implies_true]

-- https://plfa.github.io/Untyped/#contexts
abbrev Context : Type := List Ty

namespace Context
  abbrev snoc (Î“ : Context) (a : Ty) : Context := a :: Î“
  abbrev lappend (Î“ : Context) (Î” : Context) : Context := Î” ++ Î“
end Context

namespace Notation
  open Context

  -- `â€š` is not a comma! See: <https://www.compart.com/en/unicode/U+201A>
  scoped infixl:50 "â€š " => snoc
  scoped infixl:45 "â€šâ€š " => lappend
end Notation

-- https://plfa.github.io/Untyped/#exercise-context%E2%84%95-practice
instance Context.equiv_nat : Context â‰ƒ â„• where
  toFun := List.length
  invFun := (List.replicate Â· âœ¶)
  left_inv := left_inv
  right_inv := by intro; simp only [List.length_replicate]
  where
    left_inv := by intro
    | [] => trivial
    | âœ¶ :: ss => calc List.replicate (âœ¶ :: ss).length âœ¶
      _ = List.replicate (ss.length + 1) âœ¶ := by rw [List.length_cons âœ¶ ss]
      _ = âœ¶ :: List.replicate ss.length âœ¶ := by rw [List.replicate_succ âœ¶ ss.length]
      _ = âœ¶ :: ss := by have := left_inv ss; simp_all only

instance : Coe â„• Context where coe := Context.equiv_nat.invFun

-- https://plfa.github.io/Untyped/#variables-and-the-lookup-judgment
inductive Lookup : Context â†’ Ty â†’ Type where
| z : Lookup (Î“â€š t) t
| s : Lookup Î“ t â†’ Lookup (Î“â€š t') t
deriving DecidableEq

namespace Notation
  open Lookup

  scoped infix:40 " âˆ‹ " => Lookup

  -- https://github.com/arthurpaulino/lean4-metaprogramming-book/blob/d6a227a63c55bf13d49d443f47c54c7a500ea27b/md/main/macros.md#simplifying-macro-declaration
  scoped syntax "get_elem" (ppSpace term) : term
  scoped macro_rules | `(term| get_elem $n) => match n.1.toNat with
  | 0 => `(term| Lookup.z)
  | n+1 => `(term| Lookup.s (get_elem $(Lean.quote n)))

  scoped macro "â™¯" n:term:90 : term => `(get_elem $n)
end Notation

def Lookup.toNat : (Î“ âˆ‹ a) â†’ â„•
| .z => 0
| .s i => i.toNat + 1

instance : Repr (Î“ âˆ‹ a) where reprPrec i n := "â™¯" ++ reprPrec i.toNat n

-- https://plfa.github.io/Untyped/#terms-and-the-scoping-judgment
inductive Term : Context â†’ Ty â†’ Type where
-- Lookup
| var : Î“ âˆ‹ a â†’ Term Î“ a
-- Lambda
| lam : Term (Î“â€š âœ¶ /- a -/) âœ¶ /- b -/ â†’ Term Î“ âœ¶ /- (a =â‡’ b) -/
| ap : Term Î“ âœ¶ /- (a =â‡’ b) -/ â†’ Term Î“ âœ¶ /- a -/ â†’ Term Î“ âœ¶ /- b -/
deriving DecidableEq, Repr

namespace Notation
  open Term

  scoped infix:40 " âŠ¢ " => Term

  scoped prefix:50 "Æ› " => lam
  scoped infixr:min " $ " => ap
  scoped infixl:70 " â–¡ " => ap
  scoped prefix:90 "` " => var

  -- https://plfa.github.io/Untyped/#writing-variables-as-numerals
  scoped macro "#" n:term:90 : term => `(`â™¯$n)
end Notation

namespace Term
  -- https://plfa.github.io/Untyped/#test-examples
  abbrev twoC : Î“ âŠ¢ âœ¶ := Æ› Æ› (#1 $ #1 $ #0)
  abbrev fourC : Î“ âŠ¢ âœ¶ := Æ› Æ› (#1 $ #1 $ #1 $ #1 $ #0)
  abbrev addC : Î“ âŠ¢ âœ¶ := Æ› Æ› Æ› Æ› (#3 â–¡ #1 $ #2 â–¡ #1 â–¡ #0)
  abbrev fourC' : Î“ âŠ¢ âœ¶ := addC â–¡ twoC â–¡ twoC

  def church (n : â„•) : Î“ âŠ¢ âœ¶ := Æ› Æ› applyN n
  where
    applyN
    | 0 => #0
    | n + 1 => #1 â–¡ applyN n
end Term

namespace Subst
  -- https://plfa.github.io/Untyped/#renaming
  /--
  If one context maps to another,
  the mapping holds after adding the same variable to both contexts.
  -/
  def ext : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âˆ‹ a) â†’ Î“â€š b âˆ‹ a â†’ Î”â€š b âˆ‹ a := by
    intro Ï; intro
    | .z => exact .z
    | .s x => refine .s ?_; exact Ï x

  /--
  If one context maps to another,
  then the type judgements are the same in both contexts.
  -/
  def rename : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âˆ‹ a) â†’ Î“ âŠ¢ a â†’ Î” âŠ¢ a := by
    intro Ï; intro
    | ` x => exact ` (Ï x)
    | Æ› n => exact Æ› (rename (ext Ï) n)
    | l â–¡ m => exact rename Ï l â–¡ rename Ï m

  abbrev shift : Î“ âŠ¢ a â†’ Î“â€š b âŠ¢ a := rename .s

  -- https://plfa.github.io/Untyped/#simultaneous-substitution
  def exts : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âŠ¢ a) â†’ Î“â€š b âˆ‹ a â†’ Î”â€š b âŠ¢ a := by
    intro Ïƒ; intro
    | .z => exact `.z
    | .s x => apply shift; exact Ïƒ x

  /--
  General substitution for multiple free variables.
  If the variables in one context maps to some terms in another,
  then the type judgements are the same before and after the mapping,
  i.e. after replacing the free variables in the former with (expanded) terms.
  -/
  def subst : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âŠ¢ a) â†’ Î“ âŠ¢ a â†’ Î” âŠ¢ a := by
    intro Ïƒ; intro
    | ` i => exact Ïƒ i
    | Æ› n => exact Æ› (subst (exts Ïƒ) n)
    | l â–¡ m => exact subst Ïƒ l â–¡ subst Ïƒ m

  -- https://plfa.github.io/Untyped/#single-substitution
  abbrev substâ‚Ïƒ (v : Î“ âŠ¢ b) : âˆ€ {a}, Î“â€š b âˆ‹ a â†’ Î“ âŠ¢ a := by
    introv; intro
    | .z => exact v
    | .s x => exact ` x

  /--
  Substitution for one free variable `v` in the term `n`.
  -/
  abbrev substâ‚ (v : Î“ âŠ¢ b) (n : Î“â€š b âŠ¢ a) : Î“ âŠ¢ a := by
    refine subst ?_ n; exact substâ‚Ïƒ v
end Subst

open Subst

namespace Notation
  scoped infixr:90 " â‡¸ " => substâ‚
  scoped infixl:90 " â‡· " => flip substâ‚

  scoped macro " âŸª" Ïƒ:term "âŸ« " : term => `(subst $Ïƒ)
end Notation

-- https://plfa.github.io/Untyped/#neutral-and-normal-terms
mutual
  inductive Neutral : Î“ âŠ¢ a â†’ Type
  | var : (x : Î“ âˆ‹ a) â†’ Neutral (` x)
  | ap : Neutral l â†’ Normal m â†’ Neutral (l â–¡ m)
  deriving Repr

  inductive Normal : Î“ âŠ¢ a â†’ Type
  | norm : Neutral m â†’ Normal m
  | lam : Normal n â†’ Normal (Æ› n)
  deriving Repr
end

-- instance : Coe (Neutral t) (Normal t) where coe := .norm

namespace Notation
  open Neutral Normal

  scoped prefix:60 " â€²" => Normal.norm
  scoped macro "#â€²" n:term:90 : term => `(var (â™¯$n))

  scoped prefix:50 "Æ›â‚™ " => lam
  scoped infixr:min " $â‚™ " => ap
  scoped infixl:70 " â–¡â‚™ " => ap
  scoped prefix:90 "`â‚™ " => var
end Notation

example : Normal (Term.twoC (Î“ := âˆ…)) := Æ›â‚™ Æ›â‚™ (â€²#â€²1 â–¡â‚™ (â€²#â€²1 â–¡â‚™ (â€²#â€²0)))

-- https://plfa.github.io/Untyped/#reduction-step
/--
`Reduce t t'` says that `t` reduces to `t'` via a given step.

_Note: This time there's no need to generate data out of `Reduce t t'`,
so it can just be a `Prop`._
-/
inductive Reduce : (Î“ âŠ¢ a) â†’ (Î“ âŠ¢ a) â†’ Prop where
| lamÎ² : Reduce ((Æ› n) â–¡ v) (n â‡· v)
| lamÎ¶ : Reduce n n' â†’ Reduce (Æ› n) (Æ› n')
| apÎ¾â‚ : Reduce l l' â†’ Reduce (l â–¡ m) (l' â–¡ m)
| apÎ¾â‚‚ : Reduce m m' â†’ Reduce (v â–¡ m) (v â–¡ m')

-- https://plfa.github.io/Untyped/#exercise-variant-1-practice
inductive Reduce' : (Î“ âŠ¢ a) â†’ (Î“ âŠ¢ a) â†’ Type where
| lamÎ² : Normal (Æ› n) â†’ Normal v â†’ Reduce' ((Æ› n) â–¡ v) (n â‡· v)
| lamÎ¶ : Reduce' n n' â†’ Reduce' (Æ› n) (Æ› n')
| apÎ¾â‚ : Reduce' l l' â†’ Reduce' (l â–¡ m) (l' â–¡ m)
| apÎ¾â‚‚ : Normal v â†’ Reduce' m m' â†’ Reduce' (v â–¡ m) (v â–¡ m')

-- https://plfa.github.io/Untyped/#exercise-variant-2-practice
inductive Reduce'' : (Î“ âŠ¢ a) â†’ (Î“ âŠ¢ a) â†’ Type where
| lamÎ² : Reduce'' ((Æ› n) â–¡ (Æ› v)) (n â‡· (Æ› v))
| apÎ¾â‚ : Reduce'' l l' â†’ Reduce'' (l â–¡ m) (l' â–¡ m)
| apÎ¾â‚‚ : Reduce'' m m' â†’ Reduce'' (v â–¡ m) (v â–¡ m')
/-
Reduction of `four''C` under this variant might go as far as
`Æ› Æ› (twoC â–¡ #1 $ (twoC â–¡ #1 â–¡ #0))` and get stuck,
since the next step uses `lamÎ¶` which no longer exists.
-/

-- https://plfa.github.io/Untyped/#reflexive-and-transitive-closure
/--
A reflexive and transitive closure,
defined as a sequence of zero or more steps of the underlying relation `â€”â†’`.

_Note: Since `Reduce t t' : Prop`, `Clos` can be defined directly from `Reduce`._
-/
abbrev Reduce.Clos {Î“ a} := Relation.ReflTransGen (Î± := Î“ âŠ¢ a) Reduce

namespace Notation
  -- https://plfa.github.io/DeBruijn/#reflexive-and-transitive-closure
  scoped infix:40 " â€”â†’ " => Reduce
  scoped infix:20 " â€”â†  " => Reduce.Clos
end Notation

namespace Reduce.Clos
  @[refl] abbrev refl : m â€”â†  m := .refl
  abbrev tail : (m â€”â†  n) â†’ (n â€”â†’ n') â†’ (m â€”â†  n') := .tail
  abbrev head : (m â€”â†’ n) â†’ (n â€”â†  n') â†’ (m â€”â†  n') := .head
  abbrev single : (m â€”â†’ n) â†’ (m â€”â†  n) := .single

  instance : Coe (m â€”â†’ n) (m â€”â†  n) where coe r := .single r

  instance : Trans (Î± := Î“ âŠ¢ a) Clos Clos Clos where trans := .trans
  instance : Trans (Î± := Î“ âŠ¢ a) Clos Reduce Clos where trans c r := c.tail r
  instance : Trans (Î± := Î“ âŠ¢ a) Reduce Reduce Clos where trans r r' := .tail r r'
  instance : Trans (Î± := Î“ âŠ¢ a) Reduce Clos Clos where trans r c := .head r c
end Reduce.Clos

namespace Reduce
  -- https://plfa.github.io/Untyped/#example-reduction-sequence
  open Term

  example : fourC' (Î“ := âˆ…) â€”â†  fourC := calc addC â–¡ twoC â–¡ twoC
    _ â€”â†’ (Æ› Æ› Æ› (twoC â–¡ #1 $ (#2 â–¡ #1 â–¡ #0))) â–¡ twoC := by apply_rules [apÎ¾â‚, lamÎ²]
    _ â€”â†’ Æ› Æ› (twoC â–¡ #1 $ (twoC â–¡ #1 â–¡ #0)) := by exact lamÎ²
    _ â€”â†’ Æ› Æ› ((Æ› (#2 $ #2 $ #0)) $ (twoC â–¡ #1 â–¡ #0)) := by apply_rules [lamÎ¶, apÎ¾â‚, lamÎ²]
    _ â€”â†’ Æ› Æ› (#1 $ #1 $ (twoC â–¡ #1 â–¡ #0)) := by apply_rules [lamÎ¶, lamÎ²]
    _ â€”â†’ Æ› Æ› (#1 $ #1 $ ((Æ› (#2 $ #2 $ #0)) â–¡ #0)) := by apply_rules [lamÎ¶, apÎ¾â‚, apÎ¾â‚‚, lamÎ²]
    _ â€”â†’ Æ› Æ› (#1 $ #1 $ #1 $ #1 $ #0) := by apply_rules [lamÎ¶, apÎ¾â‚, apÎ¾â‚‚, lamÎ²]
end Reduce

-- https://plfa.github.io/Untyped/#progress
/--
If a term `m` is not ill-typed, then it either is a value or can be reduced.
-/
inductive Progress (m : Î“ âŠ¢ a) where
| step : (m â€”â†’ n) â†’ Progress m
| done : Normal m â†’ Progress m

/--
If a term is well-scoped, then it satisfies progress.
-/
def Progress.progress : (m : Î“ âŠ¢ a) â†’ Progress m := open Reduce in by
  intro
  | ` x => apply done; exact â€²`â‚™ x
  | Æ› n =>
    have : sizeOf n < sizeOf (Æ› n) := by simp only [
      Term.lam.sizeOf_spec, lt_add_iff_pos_left, add_pos_iff, true_or
    ]
    match progress n with
    | .done n => apply done; exact Æ›â‚™ n
    | .step n => apply step; exact lamÎ¶ n
  | ` x â–¡ m =>
    have : sizeOf m < sizeOf (` x â–¡ m) := by simp only [
      Term.ap.sizeOf_spec, Term.var.sizeOf_spec, Ty.star.sizeOf_spec,
      lt_add_iff_pos_left, add_pos_iff, true_or, or_self
    ]
    match progress m with
    | .done m => apply done; exact â€²`â‚™x â–¡â‚™ m
    | .step m => apply step; exact apÎ¾â‚‚ m
  | (Æ› n) â–¡ m => apply step; exact lamÎ²
  | l@(_ â–¡ _) â–¡ m =>
    have : sizeOf l < sizeOf (l â–¡ m) := by simp_arith
    match progress l with
    | .step l => simp_all only [namedPattern]; apply step; exact apÎ¾â‚ l
    | .done (â€²l') =>
      simp_all only [namedPattern]; rename_i h; simp only [h.symm, Term.ap.sizeOf_spec]
      have : sizeOf m < sizeOf (l â–¡ m) := by
        aesop_subst h
        simp only [Term.ap.sizeOf_spec, lt_add_iff_pos_left, add_pos_iff, true_or, or_self]
      match progress m with
      | .done m => apply done; exact â€²l' â–¡â‚™ m
      | .step m => apply step; exact apÎ¾â‚‚ m

open Progress (progress)

-- https://plfa.github.io/Untyped/#evaluation
inductive Result (n : Î“ âŠ¢ a) where
| done (val : Normal n)
| dnf
deriving Repr

inductive Steps (l : Î“ âŠ¢ a) where
| steps : âˆ€{n : Î“ âŠ¢ a}, (l â€”â†  n) â†’ Result n â†’ Steps l

def eval (gas : â„•) (l : âˆ… âŠ¢ a) : Steps l :=
  if gas = 0 then
    âŸ¨.refl, .dnfâŸ©
  else
    match progress l with
    | .done v => .steps .refl <| .done v
    | .step r =>
      let âŸ¨rs, resâŸ© := eval (gas - 1) (by trivial)
      âŸ¨Trans.trans r rs, resâŸ©

namespace Term
  abbrev id : Î“ âŠ¢ âœ¶ := Æ› #0
  abbrev delta : Î“ âŠ¢ âœ¶ := Æ› #0 â–¡ #0
  abbrev omega : Î“ âŠ¢ âœ¶ := delta â–¡ delta

  -- https://plfa.github.io/Untyped/#naturals-and-fixpoint
  /-
  The Scott encoding:
  zero := Î» _ z => z
  succ n := Î» s _ => s n

  e.g. one = succ zero
          = Î» s _ => s zero
          = Î» s _ => s (Î» _ z => z)
  -/
  abbrev zeroS : Î“ âŠ¢ âœ¶ := Æ› Æ› #0
  abbrev succS (m : Î“ âŠ¢ âœ¶) : Î“ âŠ¢ âœ¶ := (Æ› Æ› Æ› (#1 â–¡ #2)) â–¡ m
  abbrev caseS (l : Î“ âŠ¢ âœ¶) (m : Î“ âŠ¢ âœ¶) (n : Î“â€š âœ¶ âŠ¢ âœ¶) : Î“ âŠ¢ âœ¶ := l â–¡ (Æ› n) â–¡ m

  /--
  The Y combinator: `Y f := (Î» x => f (x x)) (Î» x => f (x x))`
  -/
  abbrev mu (n : Î“â€š âœ¶ âŠ¢ âœ¶) : Î“ âŠ¢ âœ¶ := (Æ› (Æ› (#1 $ #0 $ #0)) â–¡ (Æ› (#1 $ #0 $ #0))) â–¡ (Æ› n)
end Term

namespace Notation
  open Term

  scoped prefix:50 "Î¼ " => mu
  scoped prefix:80 "Î¹ " => succS
  scoped notation "ğŸ˜" => zeroS
  scoped notation "ğŸ˜? " => caseS
end Notation

-- https://plfa.github.io/Untyped/#example
section examples
  open Term

  abbrev addS : Î“ âŠ¢ âœ¶ := Î¼ Æ› Æ› (ğŸ˜? (#1) (#0) (Î¹ (#3 â–¡ #0 â–¡ #1)))

  -- https://plfa.github.io/Untyped/#exercise-multiplication-untyped-recommended
  abbrev mulS : Î“ âŠ¢ âœ¶ := Î¼ Æ› Æ› (ğŸ˜? (#1) ğŸ˜ (addS â–¡ #1 $ #3 â–¡ #0 â–¡ #1))

  abbrev oneS : Î“ âŠ¢ âœ¶ := Î¹ ğŸ˜

  abbrev twoS : Î“ âŠ¢ âœ¶ := Î¹ Î¹ ğŸ˜
  abbrev twoS'' : Î“ âŠ¢ âœ¶ := mulS â–¡ twoS â–¡ oneS

  abbrev fourS : Î“ âŠ¢ âœ¶ := Î¹ Î¹ twoS
  abbrev fourS' : Î“ âŠ¢ âœ¶ := addS â–¡ twoS â–¡ twoS
  abbrev fourS'' : Î“ âŠ¢ âœ¶ := mulS â–¡ twoS â–¡ twoS

  abbrev evalRes (l : âˆ… âŠ¢ a) (gas := 100) := (eval gas l).3

  #eval evalRes (gas := 3) fourC'
  #eval evalRes fourC'

  #eval evalRes oneS

  #eval evalRes twoS
  #eval evalRes twoS''

  #eval evalRes fourS
  #eval evalRes fourS'
  #eval evalRes fourS''
end examples

-- https://plfa.github.io/Untyped/#multi-step-reduction-is-transitive

/-
Nothing to do.
The `Trans` instance has been automatically generated by `Relation.ReflTransGen`.
See: <https://leanprover-community.github.io/mathlib4_docs/Mathlib/Logic/Relation.html#Relation.instIsTransReflTransGen>
-/

-- https://plfa.github.io/Untyped/#multi-step-reduction-is-a-congruence
/--
LEAN is being a bit weird here.
Default structural recursion cannot be used since it depends on sizeOf,
however this won't work for `Prop`.
We have to find another way.
-/
theorem Reduce.ap_congrâ‚ (rs : l â€”â†  l') : (l â–¡ m) â€”â†  (l' â–¡ m) := by
  refine rs.head_induction_on .refl ?_
  Â· introv; intro r _ rs; refine .head ?_ rs; exact apÎ¾â‚ r


theorem Reduce.ap_congrâ‚‚ (rs : m â€”â†  m') : (l â–¡ m) â€”â†  (l â–¡ m') := by
  refine rs.head_induction_on .refl ?_
  Â· introv; intro r _ rs; refine .head ?_ rs; exact apÎ¾â‚‚ r

theorem Reduce.lam_congr (rs : n â€”â†  n') : (Æ› n â€”â†  Æ› n') := by
  refine rs.head_induction_on .refl ?_
  Â· introv; intro r _ rs; refine .head ?_ rs; exact lamÎ¶ r
