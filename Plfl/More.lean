-- https://plfa.github.io/More/

import Plfl

import Mathlib.Tactic

set_option tactic.simp.trace true

-- This module was adapted based on the original one for <https://plfa.github.io/DeBruijn/>.

-- https://plfa.github.io/More/#types
inductive Ty where
/-- Native natural type made of ðŸ˜ and Î¹. -/
| nat : Ty
/-- Primitive natural type, a simple wrapper around LEAN's own â„• type. -/
| natP : Ty
/-- Product type. -/
| prod : Ty â†’ Ty â†’ Ty
/-- Sum type. -/
| sum : Ty â†’ Ty â†’ Ty
/-- Arrow type. -/
| fn : Ty â†’ Ty â†’ Ty
/-- Unit type. -/
| unit : Ty
/-- Void type. -/
| void : Ty
/-- List type. -/
| list : Ty â†’ Ty
deriving BEq, DecidableEq, Repr

namespace Notations
  open Ty

  scoped notation "â„•t" => nat
  scoped notation "â„•p" => natP

  -- Operator overloadings for `prod` and `sum` types.
  instance : Mul Ty where mul := prod
  instance : Add Ty where add := sum

  scoped infixr:70 " =â‡’ " => fn
  scoped notation " â—¯ " => unit
  scoped notation " âˆ… " => void
end Notations

open Notations

namespace Ty
  example : Ty := (â„•t =â‡’ â„•t) =â‡’ â„•t
  example : Ty := â„•p * â„•t

  @[simp]
  theorem t_to_t'_ne_t (t t' : Ty) : (t =â‡’ t') â‰  t := by
    by_contra h; match t with
    | nat => contradiction
    | fn ta tb => injection h; have := t_to_t'_ne_t ta tb; contradiction
end Ty

-- https://plfa.github.io/DeBruijn/#contexts
abbrev Context : Type := List Ty

namespace Context
  abbrev snoc (Î“ : Context) (a : Ty) : Context := a :: Î“
  abbrev lappend (Î“ : Context) (Î” : Context) : Context := Î” ++ Î“
end Context

namespace Notations
  open Context

  -- `â€š` is not a comma! See: <https://www.compart.com/en/unicode/U+201A>
  scoped infixl:50 "â€š " => snoc
  scoped infixl:45 "â€šâ€š " => lappend
end Notations

-- https://plfa.github.io/DeBruijn/#variables-and-the-lookup-judgment
inductive Lookup : Context â†’ Ty â†’ Type where
| z : Lookup (Î“â€š t) t
| s : Lookup Î“ t â†’ Lookup (Î“â€š t') t
deriving DecidableEq, Repr

namespace Notations
  open Lookup

  scoped infix:40 " âˆ‹ " => Lookup

  -- https://github.com/arthurpaulino/lean4-metaprogramming-book/blob/d6a227a63c55bf13d49d443f47c54c7a500ea27b/md/main/macros.md#simplifying-macro-declaration
  scoped syntax "get_elem" (ppSpace term) : tactic
  scoped macro_rules | `(tactic| get_elem $n) => match n.1.toNat with
  | 0 => `(tactic | exact Lookup.z)
  | n+1 => `(tactic| apply Lookup.s; get_elem $(Lean.quote n))

  scoped macro " â™¯ " n:term:90 : term => `(by get_elem $n)
end Notations

namespace Lookup
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âˆ‹ â„•t := .z
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âˆ‹ â„•t := â™¯0
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âˆ‹ â„•t =â‡’ â„•t := .s .z
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âˆ‹ â„•t =â‡’ â„•t := â™¯1
end Lookup

-- https://plfa.github.io/DeBruijn/#terms-and-the-typing-judgment
/--
A term with typing judgement embedded in itself.
-/
inductive Term : Context â†’ Ty â†’ Type where
-- Lookup
| var : Î“ âˆ‹ a â†’ Term Î“ a
-- Lambda
| lam : Term (Î“â€š a) b â†’ Term Î“ (a =â‡’ b)
| ap : Term Î“ (a =â‡’ b) â†’ Term Î“ a â†’ Term Î“ b
-- Native natural
| zero : Term Î“ â„•t
| succ : Term Î“ â„•t â†’ Term Î“ â„•t
| case : Term Î“ â„•t â†’ Term Î“ a â†’ Term (Î“â€š â„•t) a â†’ Term Î“ a
-- Fixpoint
| mu : Term (Î“â€š a) a â†’ Term Î“ a
-- Primitive natural
| prim : â„• â†’ Term Î“ â„•p
| mulP : Term Î“ â„•p â†’ Term Î“ â„•p â†’ Term Î“ â„•p
-- Let expression
| let : Term Î“ a â†’ Term (Î“â€š a) b â†’ Term Î“ b
-- Product
| prod : Term Î“ a â†’ Term Î“ b â†’ Term Î“ (a * b)
| fst : Term Î“ (a * b) â†’ Term Î“ a
| snd : Term Î“ (a * b) â†’ Term Î“ b
-- Product (alternative formulation)
-- | caseProd : Term Î“ (a * b) â†’ Term (Î“â€š aâ€š b) c â†’ Term Î“ c
-- Sum
| left : Term Î“ a â†’ Term Î“ (a + b)
| right : Term Î“ b â†’ Term Î“ (a + b)
| caseSum : Term Î“ (a + b) â†’ Term (Î“â€š a) c â†’ Term (Î“â€š b) c â†’ Term Î“ c
-- Void
| caseVoid : Term Î“ âˆ… â†’ Term Î“ a
-- Unit
| unit : Term Î“ â—¯
-- List
| nil : Term Î“ (.list a)
| cons : Term Î“ a â†’ Term Î“ (.list a) â†’ Term Î“ (.list a)
| caseList : Term Î“ (.list a) â†’ Term Î“ b â†’ Term (Î“â€š aâ€š .list a) b â†’ Term Î“ b
deriving DecidableEq, Repr

namespace Notations
  open Term

  scoped infix:40 " âŠ¢ " => Term

  scoped prefix:50 " Æ› " => lam
  scoped prefix:50 " Î¼ " => mu
  scoped notation " ðŸ˜? " => case
  scoped infixr:min " $ " => ap
  scoped infixl:70 " â–¡ " => ap
  scoped infixl:70 " â‹„ "   => mulP
  scoped prefix:80 " Î¹ " => succ
  scoped prefix:90 " ` " => var

  scoped notation " ðŸ˜ " => zero
  scoped notation " â—¯ " => unit

  -- https://plfa.github.io/DeBruijn/#abbreviating-de-bruijn-indices
  scoped macro " # " n:term:90 : term => `(`â™¯$n)
end Notations

namespace Term
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ â„•t := #0
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ â„•t =â‡’ â„•t := #1
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ â„•t := #1 $ #0
  example : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ â„•t := #1 $ #1 $ #0
  example : âˆ…â€š â„•t =â‡’ â„•t âŠ¢ â„•t =â‡’ â„•t := Æ› (#1 $ #1 $ #0)
  example : âˆ… âŠ¢ (â„•t =â‡’ â„•t) =â‡’ â„•t =â‡’ â„•t := Æ› Æ› (#1 $ #1 $ #0)

  @[simp]
  def ofNat : â„• â†’ Î“ âŠ¢ â„•t
  | 0 => .zero
  | n + 1 => .succ <| ofNat n

  instance : Coe â„• (Î“ âŠ¢ â„•t) where coe := ofNat
  instance : OfNat (Î“ âŠ¢ â„•t) n where ofNat := ofNat n

  -- https://plfa.github.io/DeBruijn/#test-examples
  example : Î“ âŠ¢ â„•t := Î¹ Î¹ ðŸ˜
  example : Î“ âŠ¢ â„•t := 2

  @[simp] abbrev add : Î“ âŠ¢ â„•t =â‡’ â„•t =â‡’ â„•t := Î¼ Æ› Æ› (ðŸ˜? (#1) (#0) (Î¹ (#3 â–¡ #0 â–¡ #1)))
  @[simp] abbrev mul : Î“ âŠ¢ â„•t =â‡’ â„•t =â‡’ â„•t := Î¼ Æ› Æ› (ðŸ˜? (#1) ðŸ˜ (add â–¡ #1 $ #3 â–¡ #0 â–¡ #1))

  example : Î“ âŠ¢ â„•t := add â–¡ 2 â–¡ 2

  /--
  The Church numeral Ty.
  -/
  abbrev Ch (t : Ty) : Ty := (t =â‡’ t) =â‡’ t =â‡’ t

  @[simp] abbrev succC : Î“ âŠ¢ â„•t =â‡’ â„•t := Æ› Î¹ #0
  @[simp] abbrev twoC : Î“ âŠ¢ Ch a := Æ› Æ› (#1 $ #1 $ #0)
  @[simp] abbrev addC : Î“ âŠ¢ Ch a =â‡’ Ch a =â‡’ Ch a := Æ› Æ› Æ› Æ› (#3 â–¡ #1 $ #2 â–¡ #1 â–¡ #0)
  example : Î“ âŠ¢ â„•t := addC â–¡ twoC â–¡ twoC â–¡ succC â–¡ ðŸ˜

  -- https://plfa.github.io/DeBruijn/#exercise-mul-recommended
  @[simp] abbrev mulC : Î“ âŠ¢ Ch a =â‡’ Ch a =â‡’ Ch a := Æ› Æ› Æ› Æ› (#3 â–¡ (#2 â–¡ #1) â–¡ #0)

  -- https://plfa.github.io/More/#example
  example : âˆ… âŠ¢ â„•p =â‡’ â„•p := Æ› #0 â‹„ #0 â‹„ #0
end Term

namespace Subst
  -- https://plfa.github.io/DeBruijn/#renaming
  /--
  If one context maps to another,
  the mapping holds after adding the same variable to both contexts.
  -/
  @[simp]
  def ext : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âˆ‹ a) â†’ Î“â€š b âˆ‹ a â†’ Î”â€š b âˆ‹ a := by
    intro Ï; intro
    | .z => exact .z
    | .s x => refine .s ?_; exact Ï x

  /--
  If one context maps to another,
  then the type judgements are the same in both contexts.
  -/
  @[simp]
  def rename : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âˆ‹ a) â†’ Î“ âŠ¢ a â†’ Î” âŠ¢ a := by
    intro Ï; intro
    | ` x => exact ` (Ï x)
    | Æ› n => exact Æ› (rename (ext Ï) n)
    | l â–¡ m => exact rename Ï l â–¡ rename Ï m
    | ðŸ˜ => exact ðŸ˜
    | Î¹ n => exact Î¹ (rename Ï n)
    | ðŸ˜? l m n => exact ðŸ˜? (rename Ï l) (rename Ï m) (rename (ext Ï) n)
    | Î¼ n => exact Î¼ (rename (ext Ï) n)
    | .prim n => exact .prim n
    | m â‹„ n => exact rename Ï m â‹„ rename Ï n
    | .let m n => exact .let (rename Ï m) (rename (ext Ï) n)
    | .prod m n => exact .prod (rename Ï m) (rename Ï n)
    | .fst n => exact .fst (rename Ï n)
    | .snd n => exact .snd (rename Ï n)
    | .left n => exact .left (rename Ï n)
    | .right n => exact .right (rename Ï n)
    | .caseSum s l r => exact .caseSum (rename Ï s) (rename (ext Ï) l) (rename (ext Ï) r)
    | .caseVoid v => exact .caseVoid (rename Ï v)
    | â—¯ => exact â—¯
    | .nil => exact .nil
    | .cons m n => exact .cons (rename Ï m) (rename Ï n)
    | .caseList l m n => exact .caseList (rename Ï l) (rename Ï m) (rename (ext (ext Ï)) n)

  abbrev shift : Î“ âŠ¢ a â†’ Î“â€š b âŠ¢ a := rename .s

  example
  : let m : âˆ…â€š â„•t =â‡’ â„•t âŠ¢ â„•t =â‡’ â„•t := Æ› (#1 $ #1 $ #0)
    let m' : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ â„•t =â‡’ â„•t := Æ› (#2 $ #2 $ #0)
    shift m = m'
  := rfl

  -- https://plfa.github.io/DeBruijn/#simultaneous-substitution
  /--
  If the variables in one context maps to some terms in another,
  the mapping holds after adding the same variable to both contexts.
  -/
  @[simp]
  def exts : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âŠ¢ a) â†’ Î“â€š b âˆ‹ a â†’ Î”â€š b âŠ¢ a := by
    intro Ïƒ; intro
    | .z => exact `.z
    | .s x => apply shift; exact Ïƒ x

  /--
  General substitution for multiple free variables.
  If the variables in one context maps to some terms in another,
  then the type judgements are the same before and after the mapping,
  i.e. after replacing the free variables in the former with (expanded) terms.
  -/
  def subst : (âˆ€ {a}, Î“ âˆ‹ a â†’ Î” âŠ¢ a) â†’ Î“ âŠ¢ a â†’ Î” âŠ¢ a := by
    intro Ïƒ; intro
    | ` i => exact Ïƒ i
    | Æ› n => exact Æ› (subst (exts Ïƒ) n)
    | l â–¡ m => exact subst Ïƒ l â–¡ subst Ïƒ m
    | ðŸ˜ => exact ðŸ˜
    | Î¹ n => exact Î¹ (subst Ïƒ n)
    | ðŸ˜? l m n => exact ðŸ˜? (subst Ïƒ l) (subst Ïƒ m) (subst (exts Ïƒ) n)
    | Î¼ n => exact Î¼ (subst (exts Ïƒ) n)
    | .prim n => exact .prim n
    | m â‹„ n => exact subst Ïƒ m â‹„ subst Ïƒ n
    | .let m n => exact .let (subst Ïƒ m) (subst (exts Ïƒ) n)
    | .prod m n => exact .prod (subst Ïƒ m) (subst Ïƒ n)
    | .fst n => exact .fst (subst Ïƒ n)
    | .snd n => exact .snd (subst Ïƒ n)
    | .left n => exact .left (subst Ïƒ n)
    | .right n => exact .right (subst Ïƒ n)
    | .caseSum s l r => exact .caseSum (subst Ïƒ s) (subst (exts Ïƒ) l) (subst (exts Ïƒ) r)
    | .caseVoid v => exact .caseVoid (subst Ïƒ v)
    | â—¯ => exact â—¯
    | .nil => exact .nil
    | .cons m n => exact .cons (subst Ïƒ m) (subst Ïƒ n)
    | .caseList l m n => exact .caseList (subst Ïƒ l) (subst Ïƒ m) (subst (exts (exts Ïƒ)) n)

  abbrev substâ‚Ïƒ (v : Î“ âŠ¢ b) : âˆ€ {a}, Î“â€š b âˆ‹ a â†’ Î“ âŠ¢ a := by
    introv; intro
    | .z => exact v
    | .s x => exact ` x

  /--
  Substitution for one free variable `v` in the term `n`.
  -/
  @[simp]
  abbrev substâ‚ (v : Î“ âŠ¢ b) (n : Î“â€š b âŠ¢ a) : Î“ âŠ¢ a := by
    refine subst ?_ n; exact substâ‚Ïƒ v

  /--
  Substitution for one two variable `v` and `w'` in the term `n`.
  -/
  @[simp]
  abbrev substâ‚‚ (v : Î“ âŠ¢ b) (w : Î“ âŠ¢ c) (n : Î“â€š bâ€š c âŠ¢ a) : Î“ âŠ¢ a := by
    refine subst ?_ n; introv; intro
    | .z => exact w
    | .s .z => exact v
    | .s (.s x) => exact ` x
end Subst

namespace Notations
  open Subst

  scoped infixr:90 " â‡´ " => substâ‚
  scoped infixl:90 " â¬° " => flip substâ‚
end Notations

open Subst

namespace Subst
  example
  : let m : âˆ… âŠ¢ â„•t =â‡’ â„•t := Æ› (Î¹ #0)
    let m' : âˆ…â€š â„•t =â‡’ â„•t âŠ¢ â„•t =â‡’ â„•t := Æ› (#1 $ #1 $ #0)
    let n : âˆ… âŠ¢ â„•t =â‡’ â„•t := Æ› (Æ› Î¹ #0) â–¡ ((Æ› Î¹ #0) â–¡ #0)
    m â‡´ m' = n
  := rfl

  example
  : let m : âˆ…â€š â„•t =â‡’ â„•t âŠ¢ â„•t := #0 $ ðŸ˜
    let m' : âˆ…â€š â„•t =â‡’ â„•tâ€š â„•t âŠ¢ (â„•t =â‡’ â„•t) =â‡’ â„•t := Æ› (#0 $ #1)
    let n : âˆ…â€š â„•t =â‡’ â„•t âŠ¢ (â„•t =â‡’ â„•t) =â‡’ â„•t := Æ› (#0 $ #1 $ ðŸ˜)
    m â‡´ m' = n
  := rfl
end Subst

inductive Value : Î“ âŠ¢ a â†’ Type where
| lam : Value (Æ› (n : Î“â€š a âŠ¢ b))
| zero : Value ðŸ˜
| succ : Value n â†’ Value (Î¹ n)
| prim : (n : â„•) â†’ Value (@Term.prim Î“ n)
| prod : Value (v : Î“ âŠ¢ a) â†’ Value (w : Î“ âŠ¢ b) â†’ Value (.prod v w)
| left : Value v â†’ Value (.left v)
| right : Value v â†’ Value (.right v)
| unit : Value â—¯
| nil : Value .nil
| cons : Value (v : Î“ âŠ¢ a) â†’ Value (vs : Î“ âŠ¢ .list a) â†’ Value (.cons v vs)
deriving DecidableEq, Repr

namespace Notations
  scoped notation " VðŸ˜ " => Value.zero
end Notations

namespace Value
  @[simp]
  def ofNat : (n : â„•) â†’ @Value Î“ â„•t (Term.ofNat n)
  | 0 => VðŸ˜
  | n + 1 => succ <| ofNat n
end Value

-- https://plfa.github.io/DeBruijn/#reduction
/--
`Reduce t t'` says that `t` reduces to `t'` via a given step.
-/
inductive Reduce : (Î“ âŠ¢ a) â†’ (Î“ âŠ¢ a) â†’ Type where
| lamÎ² : Value v â†’ Reduce ((Æ› n) â–¡ v) (n â¬° v)
| apÎ¾â‚ : Reduce l l' â†’ Reduce (l â–¡ m) (l' â–¡ m)
| apÎ¾â‚‚ : Value v â†’ Reduce m m' â†’ Reduce (v â–¡ m) (v â–¡ m')
| zeroÎ² : Reduce (ðŸ˜? ðŸ˜ m n) m
| succÎ² : Value v â†’ Reduce (ðŸ˜? (Î¹ v) m n) (n â¬° v)
| succÎ¾ : Reduce m m' â†’ Reduce (Î¹ m) (Î¹ m')
| caseÎ¾ : Reduce l l' â†’ Reduce (ðŸ˜? l m n) (ðŸ˜? l' m n)
| muÎ² : Reduce (Î¼ n) (n â¬° (Î¼ n))
-- https://plfa.github.io/More/#reduction
| mulPÎ¾â‚ : Reduce l l' â†’ Reduce (l â‹„ m) (l' â‹„ m)
| mulPÎ¾â‚‚ : Reduce m m' â†’ Reduce (l â‹„ m) (l â‹„ m')
| mulPÎ´ : Reduce ((.prim c) â‹„ (.prim d)) (.prim (c * d))
-- https://plfa.github.io/More/#reduction-1
| letÎ¾ : Reduce m m' â†’ Reduce (.let m n) (.let m' n)
| letÎ² : Value v â†’ Reduce (.let v n) (n â¬° v)
-- https://plfa.github.io/More/#reduction-2
| prodÎ¾â‚ : Reduce m m' â†’ Reduce (.prod m n) (.prod m' n)
| prodÎ¾â‚‚ : Reduce n n' â†’ Reduce (.prod m n) (.prod m n')
| fstÎ¾ : Reduce l l' â†’ Reduce (.fst l) (.fst l')
| fstÎ² : Value v â†’ Value w â†’ Reduce (.fst (.prod v w)) v
| sndÎ¾ : Reduce l l' â†’ Reduce (.snd l) (.snd l')
| sndÎ² : Value v â†’ Value w â†’ Reduce (.snd (.prod v w)) w
-- https://plfa.github.io/More/#reduction-3
-- | caseProdÎ¾ : Reduce l l' â†’ Reduce (.caseProd l m) (.caseProd l' m)
-- | caseProdÎ²
-- : Value (v : Î“ âŠ¢ a)
-- â†’ Value (w : Î“ âŠ¢ b)
-- â†’ Reduce (.caseProd (.prod v w) (m : Î“â€š aâ€š b âŠ¢ c)) (substâ‚‚ v w m)
-- https://plfa.github.io/More/#reduction-4
| caseSumÎ¾ : Reduce s s' â†’ Reduce (.caseSum s l r) (.caseSum s' l r)
| leftÎ¾ : Reduce m m' â†’ Reduce (.left m) (.left m')
| leftÎ² : Value v â†’ Reduce (.caseSum (.left v) l r) (l â¬° v)
| rightÎ¾ : Reduce m m' â†’ Reduce (.right m) (.right m')
| rightÎ² : Value v â†’ Reduce (.caseSum (.right v) l r) (r â¬° v)
-- https://plfa.github.io/More/#reduction-7
| caseVoidÎ¾ : Reduce l l' â†’ Reduce (.caseVoid l) (.caseVoid l')
-- https://plfa.github.io/More/#reduction-8
| caseListÎ¾ : Reduce l l' â†’ Reduce (.caseList l m n) (.caseList l' m n)
| nilÎ² : Reduce (.caseList .nil m n) m
| consÎ¾â‚ : Reduce m m' â†’ Reduce (.cons m n) (.cons m' n)
| consÎ¾â‚‚ : Reduce n n' â†’ Reduce (.cons v n) (.cons v n')
| consÎ² : Reduce (.caseList (.cons v w) m n) (substâ‚‚ v w n)

/--
The predicate version of `Reduce`.
-/
abbrev Reduce.ReduceP (t : Î“ âŠ¢ a) (t' : Î“ âŠ¢ a) := Nonempty (Reduce t t')

namespace Notations
  -- https://plfa.github.io/DeBruijn/#reflexive-and-transitive-closure
  scoped infix:40 " â€”â†’ " => Reduce
  scoped infix:40 " â€”â†’â‚š " => Reduce.ReduceP
end Notations

namespace Reduce
  instance : Coe (m â€”â†’ n) (m â€”â†’â‚š n) where coe r := âŸ¨râŸ©

  /--
  A reflexive and transitive closure,
  defined as a sequence of zero or more steps of the underlying relation `â€”â†’`.
  -/
  abbrev Clos {Î“ a} := Relation.ReflTransGen (Î± := Î“ âŠ¢ a) ReduceP
end Reduce

namespace Notations
  scoped infix:20 " â€”â†  " => Reduce.Clos
end Notations

namespace Reduce.Clos
    @[simp] abbrev one (c : m â€”â†’ n) : (m â€”â†  n) := .tail .refl c
    instance : Coe (m â€”â†’ n) (m â€”â†  n) where coe := one

    instance : Trans (Î± := Î“ âŠ¢ a) Clos Clos Clos where
      trans := Relation.ReflTransGen.trans

    instance : Trans (Î± := Î“ âŠ¢ a) Clos Reduce Clos where
      trans c r := c.tail r

    instance : Trans (Î± := Î“ âŠ¢ a) Reduce Reduce Clos where
      trans c c' := (one c).tail c'

    instance : Trans (Î± := Î“ âŠ¢ a) Reduce Clos Clos where
      trans r c := (one r).trans c
end Reduce.Clos

namespace Reduce
  -- https://plfa.github.io/DeBruijn/#examples
  open Term

  example : twoC â–¡ succC â–¡ @zero âˆ… â€”â†  2 := calc
    twoC â–¡ succC â–¡ ðŸ˜
    _ â€”â†’ (Æ› (succC $ succC $ #0)) â–¡ ðŸ˜ := by apply apÎ¾â‚; apply lamÎ²; exact Value.lam
    _ â€”â†’ (succC $ succC $ ðŸ˜) := by apply lamÎ²; exact VðŸ˜
    _ â€”â†’ succC â–¡ 1 := by apply apÎ¾â‚‚; apply Value.lam; exact lamÎ² VðŸ˜
    _ â€”â†’ 2 := by apply lamÎ²; exact Value.ofNat 1
end Reduce

-- https://plfa.github.io/DeBruijn/#values-do-not-reduce
@[simp]
def Value.emptyReduce : Value m â†’ âˆ€ {n}, IsEmpty (m â€”â†’ n) := by
  introv v; is_empty; intro r
  cases v with try contradiction
  | succ v => cases r; Â· case succÎ¾ => apply (emptyReduce v).false; trivial
  | prod => cases r with
    | prodÎ¾â‚ r => rename_i v _ _; apply (emptyReduce v).false; trivial
    | prodÎ¾â‚‚ r => rename_i v _; apply (emptyReduce v).false; trivial
  | left v => cases r; Â· case leftÎ¾ => apply (emptyReduce v).false; trivial
  | right v => cases r; Â· case rightÎ¾ => apply (emptyReduce v).false; trivial
  | cons => cases r with
    | consÎ¾â‚ r => rename_i v _ _; apply (emptyReduce v).false; trivial
    | consÎ¾â‚‚ r => rename_i v _; apply (emptyReduce v).false; trivial

@[simp]
def Reduce.emptyValue : m â€”â†’ n â†’ IsEmpty (Value m) := by
  intro r; is_empty; intro v
  have : âˆ€ {n}, IsEmpty (m â€”â†’ n) := Value.emptyReduce v
  exact this.false r

/--
If a term `m` is not ill-typed, then it either is a value or can be reduced.
-/
@[aesop safe [constructors, cases]]
inductive Progress (m : âˆ… âŠ¢ a) where
| step : Reduce m n â†’ Progress m
| done : Value m â†’ Progress m

def Progress.progress : (m : âˆ… âŠ¢ a) â†’ Progress m := open Reduce in by
  intro
  | ` _ => contradiction
  | Æ› _ => exact .done .lam
  | l â–¡ m => match progress l with
    | .step _ => apply step; apply apÎ¾â‚; trivial
    | .done l => match progress m with
      | .step _ => apply step; apply apÎ¾â‚‚ <;> trivial
      | .done _ => match l with
        | .lam => apply step; apply lamÎ²; trivial
  | ðŸ˜ => exact .done VðŸ˜
  | Î¹ n => match progress n with
    | .step _ => apply step; apply succÎ¾; trivial
    | .done _ => apply done; apply Value.succ; trivial
  | ðŸ˜? l m n => match progress l with
    | .step _ => apply step; apply caseÎ¾; trivial
    | .done v => match v with
      | .zero => exact .step zeroÎ²
      | .succ _ => apply step; apply succÎ²; trivial
  | Î¼ _ => exact .step muÎ²
  | .prim n => exact .done (.prim n)
  | m â‹„ n => match progress m with
    | .step _ => apply step; apply mulPÎ¾â‚; trivial
    | .done m => match progress n with
      | .step _ => apply step; apply mulPÎ¾â‚‚; trivial
      | .done n => match m, n with
        | .prim m, .prim n => exact .step mulPÎ´
  | .let m n => match progress m with
    | .step _ => apply step; apply letÎ¾; trivial
    | .done m => apply step; apply letÎ²; trivial
  | .prod m n => match progress m with
    | .step _ => apply step; apply prodÎ¾â‚; trivial
    | .done m => match progress n with
      | .step _ => apply step; apply prodÎ¾â‚‚; trivial
      | .done n => exact .done (.prod m n)
  | .fst n => match progress n with
    | .step _ => apply step; apply fstÎ¾; trivial
    | .done n => match n with
      | .prod v w => apply step; apply fstÎ² <;> trivial
  | .snd n => match progress n with
    | .step _ => apply step; apply sndÎ¾; trivial
    | .done n => match n with
      | .prod v w => apply step; apply sndÎ² <;> trivial
  | .left n => match progress n with
    | .step _ => apply step; apply leftÎ¾; trivial
    | .done n => exact .done (.left n)
  | .right n => match progress n with
    | .step _ => apply step; apply rightÎ¾; trivial
    | .done n => exact .done (.right n)
  | .caseSum s l r => match progress s with
    | .step _ => apply step; apply caseSumÎ¾; trivial
    | .done s => match s with
      | .left _ => apply step; apply leftÎ²; trivial
      | .right _ => apply step; apply rightÎ²; trivial
  | .caseVoid v => match progress v with
    | .step _ => apply step; apply caseVoidÎ¾; trivial
    | .done _ => contradiction
  | â—¯ => exact .done .unit
  | .nil => exact .done .nil
  | .cons m n => match progress m with
    | .step _ => apply step; apply consÎ¾â‚; trivial
    | .done _ => match progress n with
      | .step _ => apply step; apply consÎ¾â‚‚; trivial
      | .done _ => refine .done (.cons ?_ ?_) <;> trivial
  | .caseList l m n => match progress l with
    | .step _ => apply step; apply caseListÎ¾; trivial
    | .done l => match l with
      | .nil => apply step; exact nilÎ²
      | .cons _ w => apply step; exact consÎ²

open Progress (progress)

inductive Result (n : Î“ âŠ¢ a) where
| done (val : Value n)
| dnf
deriving BEq, DecidableEq, Repr

inductive Steps (l : Î“ âŠ¢ a) where
| steps : âˆ€{n : Î“ âŠ¢ a}, (l â€”â†  n) â†’ Result n â†’ Steps l

@[simp]
def eval (gas : â„•) (l : âˆ… âŠ¢ a) : Steps l :=
  if gas = 0 then
    âŸ¨.refl, .dnfâŸ©
  else
    match progress l with
    | .done v => .steps .refl <| .done v
    | .step r =>
      let âŸ¨rs, resâŸ© := eval (gas - 1) (by trivial)
      âŸ¨Trans.trans r rs, resâŸ©

section examples
  open Term

  -- def x : â„• := x + 1
  abbrev succÎ¼ : âˆ… âŠ¢ â„•t := Î¼ Î¹ #0

  abbrev evalRes (l : âˆ… âŠ¢ a) (gas := 100) := (eval gas l).3

  #eval evalRes (gas := 3) succÎ¼
  #eval evalRes <| add â–¡ 2 â–¡ 1
  #eval evalRes <| mul â–¡ 2 â–¡ 2
  -- Prim
  #eval evalRes <| .prim 2 â‹„ .prim 3
  -- Let
  #eval evalRes <| .let (.prim 6) (#0 â‹„ .prim 7)
  #eval evalRes <| .let (.prim 3) <| .let (.prim 4) (.prod (#1) (#0))
  -- Prod, Unit
  #eval evalRes <| .fst <| .snd <| .prod â—¯ (.prod (.prim 6) (Î¹ Î¹ 0))
  -- Sum
  #eval evalRes <| (.left (.prim 3) : âˆ… âŠ¢ â„•p + â„•t)
  #eval evalRes <| (.right 4 : âˆ… âŠ¢ â„•p + â„•t)
  #eval evalRes <| .caseSum (.right 1 : âˆ… âŠ¢ â„•p + â„•t) ðŸ˜ (.succ (#0))
  -- List
  #eval evalRes <| .nil (a := â„•t)
  #eval evalRes <| .cons (Î¹ ðŸ˜) <| .cons ðŸ˜ .nil
  #eval evalRes <| .caseList (.cons (Î¹ ðŸ˜) <| .cons ðŸ˜ .nil) ðŸ˜ (#1 /- 0:cdr, 1:car -/)
end examples
