-- https://plfa.github.io/Compositional/

import Plfl.Init
import Plfl.Untyped.Denotational

namespace Compositional

open Untyped.Notation
open Denotational
open Denotational.Notation

-- https://plfa.github.io/Compositional/#equation-for-lambda-abstraction
def â„± (d : Denot (Î“â€š âœ¶)) : Denot Î“
| _, âŠ¥ => âŠ¤
| Î³, v â‡¾ w => d (Î³`â€š v) w
| Î³, .conj u v => â„± d Î³ u âˆ§ â„± d Î³ v

lemma sub_â„± (d : â„± (â„° n) Î³ v) (lt : u âŠ‘ v) : â„± (â„° n) Î³ u := by induction lt with
| bot => triv
| conjL _ _ ih ih' => exact âŸ¨ih d, ih' dâŸ©
| conjRâ‚ _ ih => exact ih d.1
| conjRâ‚‚ _ ih => exact ih d.2
| trans _ _ ih ih' => exact ih (ih' d);
| fn lt lt' => exact .sub (up_env d lt) lt'
| dist => exact .conj d.1 d.2

lemma â„±_â„° (d : â„° (Æ› n) Î³ v) : â„± (â„° n) Î³ v := by
  generalize hx : (Æ› n) = x at *
  induction d with try injection hx
  | fn d => subst_vars; exact d
  | bot => triv
  | conj _ _ ih ih' => exact âŸ¨ih hx, ih' hxâŸ©
  | sub _ lt ih => exact sub_â„± (ih hx) lt

theorem lam_inv (d : Î³ âŠ¢ Æ› n â‡“ v â‡¾ v') : (Î³`â€š v) âŠ¢ n â‡“ v' := â„±_â„° d

lemma â„°_lam (d : â„± (â„° n) Î³ v) : â„° (Æ› n) Î³ v := match v with
| .bot => .bot
| .fn _ _ => .fn d
| .conj _ _ => (â„°_lam d.1).conj (â„°_lam d.2)

theorem lam_equiv : â„° (Æ› n) = â„± (â„° n) := by ext; exact âŸ¨â„±_â„°, â„°_lamâŸ©

-- https://plfa.github.io/Compositional/#equation-for-function-application
abbrev ğ’œ (d d' : Denot Î“) : Denot Î“ | Î³, w => (w âŠ‘ âŠ¥) âˆ¨ (âˆƒ v, d Î³ (v â‡¾ w) âˆ§ d' Î³ v)

namespace Notation
  scoped infixl:70 " â— " => ğ’œ
end Notation

open Notation

lemma ğ’œ_â„° (d : â„° (l â–¡ m) Î³ v) : (â„° l â— â„° m) Î³ v := by
  generalize hx : l â–¡ m = x at *
  induction d with try injection hx
  | bot => left; rfl
  | ap d d' => subst_vars; right; rename_i v' _ _ _ _; exists v'
  | sub _ lt ih => match ih hx with
    | .inl lt' => left; exact lt.trans lt'
    | .inr âŸ¨v', efv', ev'âŸ© => right; refine âŸ¨v', efv'.sub ?_, ev'âŸ©; exact .fn .refl lt
  | conj _ _ ih ih' => match ih hx, ih' hx with
    | .inl lt, .inl lt' => left; exact lt.conjL lt'
    | .inl lt, .inr âŸ¨v', efv', ev'âŸ© =>
        right; refine âŸ¨v', efv'.sub ?_, ev'âŸ©; refine .fn .refl ?_
        refine .conjL ?_ .refl; exact sub_of_sub_bot lt
    | .inr âŸ¨v', efv', ev'âŸ©, .inl lt =>
        right; refine âŸ¨v', efv'.sub ?_, ev'âŸ©; refine .fn .refl ?_
        refine .conjL .refl ?_; exact sub_of_sub_bot lt
    | .inr âŸ¨v', efv', ev'âŸ©, .inr âŸ¨v'', efv'', ev''âŸ© =>
        right; refine âŸ¨v' âŠ” v'', ?_, ev'.conj ev''âŸ©
        exact (efv'.conj efv'').sub fn_conj_sub_conj_fn

lemma â„°_ap : (â„° l â— â„° m) Î³ v â†’ â„° (l â–¡ m) Î³ v
| .inl lt => .sub .bot lt
| .inr âŸ¨_, efv, evâŸ© => efv.ap ev

theorem ap_equiv : â„° (l â–¡ m) = (â„° l â— â„° m) := by ext; exact âŸ¨ğ’œ_â„°, â„°_apâŸ©

abbrev ğ’± (i : Î“ âˆ‹ âœ¶) (Î³ : Env Î“) (v : Value) : Prop := v âŠ‘ Î³ i

theorem var_inv (d : â„° (` i) Î³ v) : ğ’± i Î³ v := by
  generalize hx : (` i) = x at *
  induction d with try injection hx
  | var => subst_vars; rfl
  | bot => exact .bot
  | conj _ _ ih ih' => exact (ih hx).conjL (ih' hx)
  | sub _ lt ih => exact lt.trans (ih hx)

theorem var_equiv : â„° (` i) = ğ’± i := by ext; exact âŸ¨var_inv, .sub .varâŸ©

-- https://plfa.github.io/Compositional/#congruence
lemma lam_congr (h : â„° n = â„° n') : â„° (Æ› n) = â„° (Æ› n') := calc _
  _ = â„± (â„° n) := lam_equiv
  _ = â„± (â„° n') := by rw [h]
  _ = â„° (Æ› n') := lam_equiv.symm

lemma ap_congr (hl : â„° l = â„° l') (hm : â„° m = â„° m') : â„° (l â–¡ m) = â„° (l' â–¡ m') := calc _
  _ = â„° l â— â„° m := ap_equiv
  _ = â„° l' â— â„° m' := by rw [hl, hm]
  _ = â„° (l' â–¡ m') := ap_equiv.symm

-- https://plfa.github.io/Compositional/#compositionality
open Untyped (Context)

/--
`Holed Î“ Î”` describes a program with a hole in it:
- `Î“` is the `Context` for the hole.
- `Î”` is the `Context` for the terms that result from filling the hole.
-/
inductive Holed : Context â†’ Context â†’ Type where
/-- A basic hole. -/
| hole : Holed Î“ Î“
/-- Î»-abstracting the hole makes a bigger hole. -/
| lam : Holed (Î“â€š âœ¶) (Î”â€š âœ¶) â†’ Holed (Î“â€š âœ¶) Î”
/-- Applying to a holed function makes a bigger hole. -/
| apL : Holed Î“ Î” â†’ (Î” âŠ¢ âœ¶) â†’ Holed Î“ Î”
/-- Applying a holed argument makes a bigger hole. -/
| apR : (Î” âŠ¢ âœ¶) â†’ Holed Î“ Î” â†’ Holed Î“ Î”

/-- `plug`s a term into a `Holed` context, making a new term. -/
def Holed.plug : Holed Î“ Î” â†’ (Î“ âŠ¢ âœ¶) â†’ (Î” âŠ¢ âœ¶)
| .hole, m => m
| .lam c, n => Æ› c.plug n
| .apL c n, l => c.plug l â–¡ n
| .apR l c, m => l â–¡ c.plug m

theorem compositionality {c : Holed Î“ Î”} (h : â„° m = â„° n) : â„° (c.plug m) = â„° (c.plug n) := by
  induction c with unfold Holed.plug
  | hole => exact h
  | lam _ ih => exact lam_congr (ih h)
  | apL _ _ ih => exact ap_congr (ih h) (by rfl)
  | apR _ _ ih => exact ap_congr (by rfl) (ih h)

-- https://plfa.github.io/Compositional/#the-denotational-semantics-defined-as-a-function
/--
`â„°â‚€ m` is the instance of `Denot` that corresponds to the `Eval` of `m`.
It is like `â„° m`, but defined computationally.
-/
def â„°â‚€ : (Î“ âŠ¢ âœ¶) â†’ Denot Î“
| ` i => ğ’± i
| Æ› n => â„± (â„°â‚€ n)
| l â–¡ m => â„°â‚€ l â— â„°â‚€ m

/-- The two definitions of `â„°` are equivalent. -/
theorem â„°_eq_â„°â‚€ : â„° (Î“ := Î“) = â„°â‚€ := by ext; rw [impl]
  where
    impl {a} {m : Î“ âŠ¢ a} : â„° m = â„°â‚€ m := by
      induction m with (ext Î³ v; simp only [â„°â‚€])
      | var i => rw [var_equiv]
      | lam n ih => rw [â†ih, lam_equiv]
      | ap l m ih ih' => rw [â†ih, â†ih', ap_equiv]
